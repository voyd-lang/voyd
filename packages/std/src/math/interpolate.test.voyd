use std::test::assertions::all

test "clamp and between support positional and labeled overloads":
  assert(clamp(7, 1, 5), eq: 5)
  assert(clamp(2, min: 5, max: 1), eq: 2)
  assert(clamp(0, min: 1, max: 5), eq: 1)

  assert(between(3, 1, 5), eq: true)
  assert(between(6, min: 1, max: 5), eq: false)
  assert(between(3, min: 5, max: 1), eq: true)

test "float overloads clamp and between":
  assert(clamp(3.75, 1.0, 3.0), eq: 3.0)
  assert(clamp(f32_from_f64(0.25), min: f32_from_f64(0.5), max: f32_from_f64(2.0)), eq: f32_from_f64(0.5))

  assert(between(2.5, 2.0, 3.0), eq: true)
  assert(between(f32_from_f64(4.0), min: f32_from_f64(1.0), max: f32_from_f64(3.0)), eq: false)

test "lerp inverse_lerp and map_range support overloads":
  assert(lerp(0.0, 10.0, 0.25), eq: 2.5)
  assert(lerp(0.0, to: 10.0, t: 0.25), eq: 2.5)

  assert(approx_eq(inverse_lerp(2.5, 0.0, 10.0), 0.25, 0.000000000001), eq: true)
  assert(
    approx_eq(
      inverse_lerp(2.5, start: 0.0, end: 10.0),
      0.25,
      0.000000000001
    ),
    eq: true
  )

  assert(
    approx_eq(
      map_range(5.0, in_min: 0.0, in_max: 10.0, out_min: 100.0, out_max: 200.0),
      150.0,
      0.000000000001
    ),
    eq: true
  )

  let value32 = f32_from_f64(0.25)
  assert(
    approx_eq(
      f64_from_f32(lerp(value32, to: f32_from_f64(1.0), t: f32_from_f64(0.5))),
      0.625,
      0.00001
    ),
    eq: true
  )
  assert(
    approx_eq(
      f64_from_f32(inverse_lerp(f32_from_f64(2.5), start: f32_from_f64(0.0), end: f32_from_f64(10.0))),
      0.25,
      0.00001
    ),
    eq: true
  )
  assert(
    approx_eq(
      f64_from_f32(
        map_range(
          f32_from_f64(5.0),
          in_min: f32_from_f64(0.0),
          in_max: f32_from_f64(10.0),
          out_min: f32_from_f64(100.0),
          out_max: f32_from_f64(200.0)
        )
      ),
      150.0,
      0.0001
    ),
    eq: true
  )

test "interpolation APIs support UFCS style":
  assert(0.0.lerp(to: 10.0, t: 0.5), eq: 5.0)
  assert(approx_eq(5.0.inverse_lerp(start: 0.0, end: 10.0), 0.5, 0.000000000001), eq: true)
  assert(
    approx_eq(
      5.0.map_range(in_min: 0.0, in_max: 10.0, out_min: -1.0, out_max: 1.0),
      0.0,
      0.000000000001
    ),
    eq: true
  )

fn approx_eq(left: f64, right: f64, epsilon: f64) -> bool
  let delta = left - right
  let abs_delta = if delta < 0.0 then: 0.0 - delta else: delta
  abs_delta <= epsilon

@intrinsic(name: "__f32_demote_f64")
fn f32_from_f64(value: f64): () -> f32 __f32_demote_f64(value)

@intrinsic(name: "__f64_promote_f32")
fn f64_from_f32(value: f32): () -> f64 __f64_promote_f32(value)
