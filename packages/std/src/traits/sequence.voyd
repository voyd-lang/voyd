//! Sequence and iterator traits.
//!
//! Defines iteration contracts and the `for` macro expansion used by the language.

use std::optional::all

/// Trait for values that can produce iterators.
pub trait Sequence<T>
  /// Returns an iterator over sequence items.
  fn iter(self) -> Iterator<T>

/// Trait for stateful iterators over `T` values.
pub trait Iterator<T>
  /// Produces the next item, or `None` when exhausted.
  fn next(~self) -> Option<T>

pub macro for(case)
  let cond = case.get(1)
  let do = case.get(2)
  let binding = cond.get(1)
  let iterable = cond.get(2)
  let iterator_id = identifier(__iterable)
  let iterator_val_id = identifier(__iterable_val)

  `
    let ~($iterator_id) = $iterable.iter()
    while true do:
      let $iterator_val_id = ($iterator_id).next()
      $iterator_val_id.match
        Some:
          let $binding = $iterator_val_id.value
          $do
          void
        None:
          break
