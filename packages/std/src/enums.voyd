pub macro enum(enum_name, variants_block)
  let variants = variants_block.slice(1).map((variant) =>
    if is_list(variant) then:
      variant
    else:
      `($variant {})
  )
  let first_variant = variants.get(0)
  let inferred_generics =
    if is_list(first_variant) then:
      if first_variant.length == 3 then:
        first_variant.get(1)
      else:
        variants.slice(from: 0, to: 0)
    else:
      variants.slice(from: 0, to: 0)
  let enum_head =
    if is_list(enum_name) then:
      enum_name
    else:
      if inferred_generics.length > 0 then:
        `($enum_name $inferred_generics)
      else:
        enum_name
  let variant_names = variants.map((variant) =>
    if variant.length == 3 then:
      variant.slice(0, 2)
    else:
      variant.get(0)
  )
  let object_decls = variants.map((variant) =>
    let object_head =
      if variant.length == 3 then:
        variant.slice(0, 2)
      else:
        variant.get(0)
    let object_body =
      if variant.length == 3 then:
        variant.get(2)
      else:
        variant.get(1)
    `(obj $object_head $object_body)
  )
  let first_variant_name = variant_names.get(0)
  let remaining_variants = variant_names.slice(1)
  let union_target = remaining_variants.reduce(
    first_variant_name,
    (acc, next) => `($acc | $next)
  )
  let declarations = object_decls.push(`(type ($enum_head = $union_target)))

  emit_many(declarations)
