use std::error::HostError
use std::msgpack::MsgPack
use std::msgpack::self as msgpack
use std::optional::types::all
use std::result::types::all
use std::string::type::String
use std::test::assertions::all
use std::test::host_dto::self as host_dto_test

test "env get resolves same-name wrapper and decodes strings":
  let result = get_with_payload(string_payload("voyd"))
  match(result)
    Some<String> { value }:
      assert(value.equals("voyd"), eq: true)
    None:
      assert(false, eq: true)

test "env get helpers parse boolean and integer":
  let bool_result = get_boolean_with_payload(string_payload("TRUE"))
  match(bool_result)
    Some<bool> { value }:
      assert(value, eq: true)
    None:
      assert(false, eq: true)

  let int_result = get_integer_with_payload(string_payload("42"))
  match(int_result)
    Some<i32> { value }:
      assert(value, eq: 42)
    None:
      assert(false, eq: true)

test "env set decodes host errors":
  let result = set_with_payload(host_dto_test::host_error_payload(code: 9, message: "denied"))
  match(result)
    Err<HostError> { error }:
      assert(error.code, eq: 9)
      assert(error.message.equals("denied"), eq: true)
    Ok<Unit>:
      assert(false, eq: true)

fn get_with_payload(payload: MsgPack): () -> Option<String>
  try
    get("APP_NAME")
  Env::get(tail, _key):
    tail(payload)
  Env::set(tail, _payload):
    tail(host_dto_test::host_error_payload(code: -1, message: "unused"))

fn get_boolean_with_payload(payload: MsgPack): () -> Option<bool>
  try
    get_boolean("ENABLED")
  Env::get(tail, _key):
    tail(payload)
  Env::set(tail, _payload):
    tail(host_dto_test::host_error_payload(code: -1, message: "unused"))

fn get_integer_with_payload(payload: MsgPack): () -> Option<i32>
  try
    get_integer("PORT")
  Env::get(tail, _key):
    tail(payload)
  Env::set(tail, _payload):
    tail(host_dto_test::host_error_payload(code: -1, message: "unused"))

fn set_with_payload(payload: MsgPack): () -> Result<Unit, HostError>
  try
    set("PORT", "8080")
  Env::get(tail, _key):
    tail(msgpack::make_null())
  Env::set(tail, _payload):
    tail(payload)

fn string_payload(value: String): () -> MsgPack
  msgpack::make_string(value)
