//! Optional helper functions.
//!
//! Constructors, predicates, and combinators for `Optional<T>` values.

use super::types::all

/// Constructs an optional value containing `value`.
pub fn some<T>(
  /// Value to wrap in `Some`.
  value: T
): () -> Optional<T>
  Some { value: value }

/// Constructs an empty optional value.
pub fn none<T>(): () -> Optional<T>
  None {}

/// Returns true when the optional value is present.
pub fn is_some<T>(
  /// Optional value to inspect.
  opt: Optional<T>
): () -> boolean
  match(opt)
    Some:
      true
    None:
      false

/// Returns true when the optional value is absent.
pub fn is_none<T>(
  /// Optional value to inspect.
  opt: Optional<T>
): () -> boolean
  match(opt)
    Some:
      false
    None:
      true

/// Returns the contained value or a fallback.
pub fn unwrap_or<T>(
  /// Optional value to unwrap.
  opt: Optional<T>,
  /// Fallback value returned for `None`.
  default: T
): () -> T
  match(opt)
    Some { value }:
      value
    None:
      default

/// Returns the contained value or computes a fallback lazily.
pub fn unwrap_or_else<T>(
  /// Optional value to unwrap.
  opt: Optional<T>,
  /// Function invoked only when `opt` is `None`.
  default: () -> T
) -> T
  match(opt)
    Some { value }:
      value
    None:
      default()

/// Transforms a present value and leaves `None` unchanged.
pub fn map<T, U>(
  /// Optional value to transform.
  opt: Optional<T>,
  /// Mapping function applied to `Some` payloads.
  f: (v: T) -> U
) -> Optional<U>
  match(opt)
    Some { value }:
      Some { value: f(value) }
    None:
      None {}

/// Runs `f` when a value is present and flattens the result.
pub fn and_then<T, U>(
  /// Optional value to transform.
  opt: Optional<T>,
  /// Function applied to `Some` payloads.
  f: (v: T) -> Optional<U>
) -> Optional<U>
  match(opt)
    Some { value }:
      f(value)
    None:
      None {}

/// Returns `opt` when present, otherwise `fallback`.
pub fn or_value<T>(
  /// Primary optional value.
  opt: Optional<T>,
  /// Fallback optional value used for `None`.
  fallback: Optional<T>
): () -> Optional<T>
  match(opt)
    Some:
      opt
    None:
      fallback

/// Returns `opt` when present, otherwise computes a fallback optional value.
pub fn or_else<T>(
  /// Primary optional value.
  opt: Optional<T>,
  /// Function invoked to compute a fallback for `None`.
  fallback: () -> Optional<T>
) -> Optional<T>
  match(opt)
    Some:
      opt
    None:
      fallback()

pub macro '??'(l, r)
  let l_result = identifier(__optional_coalesce_left)
  `
    let $l_result = $l
    if $l_result is Some:
      $l_result.value
    else:
      $r

pub macro '?.'(l, r)
  let l_result = identifier(__optional_chain_left)
  let value = identifier(__optional_chain_value)
  `
    let $l_result = $l
    match($l_result)
      Some { value: $value }:
        Some { value: ($value).$r }
      None:
        None {}
